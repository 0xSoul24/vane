plugins {
	id 'com.github.johnrengelman.shadow' version '6.0.0';
}

dependencies {
	implementation group: 'io.github.waterfallmc', name: 'waterfall-api', version: '1.16-R0.4-SNAPSHOT';
	implementation group: 'org.bstats', name: 'bstats-bungeecord', version: '1.7';
	implementation group: 'org.json', name: 'json', version: '20200518';
}

processResources {
	filesMatching("**/bungee.yml") {
		expand(project.properties)
	}
}

shadowJar {
	dependencies {
		include(dependency('org.bstats:bstats-bungeecord'));
		include(dependency('org.json:json'));
	}
	relocate 'org.bstats', 'org.oddlama.vane.vane_waterfall.external.bstats';
	relocate 'org.json', 'org.oddlama.vane.vane_waterfall.external.json';
}

signing {
	if (project.hasProperty('signing.gnupg.keyName')) {
		sign shadowJar;
	}
}
build.dependsOn signShadowJar;

task copyJar(type: Copy) {
	from shadowJar;
	into "${project.rootProject.projectDir}/target-waterfall";
	duplicatesStrategy 'include';
	rename '(.*)-all.jar', '$1.jar';
}
build.dependsOn copyJar;

task copyJarAsc(type: Copy) {
	from signShadowJar;
	into "${project.rootProject.projectDir}/target-waterfall";
	duplicatesStrategy 'include';
	rename '(.*)-all.jar.asc', '$1.jar.asc';
}
build.dependsOn copyJarAsc;
