plugins {
	id 'org.ajoberstar.organize-imports' version '1.0.3' apply false;
}

subprojects {
	apply plugin: 'java';
	apply plugin: 'org.ajoberstar.organize-imports';

	group 'org.oddlama.imex';
	version '1.0.0';
	sourceCompatibility = 11;

	repositories() {
		mavenCentral();
		maven { url 'https://papermc.io/repo/repository/maven-public/' };
		maven { url 'https://repo.dmulloy2.net/nexus/repository/public/' };
		maven { url 'https://repo.mikeprimm.com/' };
		maven { url 'https://papermc.io/repo/repository/maven-public/' };
	}

	compileJava {
		options.encoding = 'UTF-8';
		options.compilerArgs << '-Xlint:all';
	}
}

configure(subprojects.findAll {it.name != 'imex-annotations'}) {
	repositories {
		flatDir {
			dir rootProject.file('libs');
		}
	}

	dependencies {
		//implementation group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.1-R0.1-SNAPSHOT';
		// We need the full implementation with CraftBukkit and NMS to compile
		implementation name: 'paper-133-patched';
		implementation group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '4.6.0-SNAPSHOT';
		implementation group: 'us.dynmap', name: 'dynmap-api', version: '3.1-SNAPSHOT';

		compileOnly project(':imex-annotations');
		annotationProcessor project(':imex-annotations');
	}

	processResources {
		filesMatching("**/plugin.yml") {
			expand(project.properties)
		}
	}
}

configure(subprojects.findAll {it.name != 'imex-annotations' && it.name != 'imex-core'}) {
	dependencies {
		implementation project(':imex-core');
	}
}
