plugins {
	id 'signing';
}

subprojects {
	apply plugin: 'java';
	apply plugin: 'signing';

	group 'org.oddlama.vane';
	version '1.1.6';
	sourceCompatibility = 11;

	repositories() {
		mavenCentral();
		maven { url 'https://papermc.io/repo/repository/maven-public/' };
		maven { url 'https://repo.dmulloy2.net/nexus/repository/public/' };
		maven { url 'https://repo.mikeprimm.com/' };
		maven { url 'https://repo.codemc.org/repository/maven-public/' };
		maven { url 'https://jitpack.io' };
		flatDir {
			dir rootProject.file('libs');
		}
	}

	compileJava {
		options.encoding = 'UTF-8';
		options.compilerArgs << '-Xlint:all';
		options.compilerArgs << '-Xlint:-processing';
		options.compilerArgs << '-Xdiags:verbose';
	}

	dependencies {
		compileOnly group: 'org.jetbrains', name: 'annotations', version: '20.0.0';
		annotationProcessor 'org.jetbrains:annotations:20.0.0';
	}

	signing {
		if (project.hasProperty('signing.gnupg.keyName')) {
			useGpgCmd();
			sign jar;
		}
	}
	build.dependsOn signJar;
}

configure(subprojects.findAll {it.name != 'vane-waterfall'}) {
	dependencies {
		// We need the full implementation with CraftBukkit and NMS to compile.
		// Simply copy cache/patched_{..}.jar from a paper server into libs/.
		implementation name: 'patched_1.16.5';
	}
}

configure(subprojects.findAll {it.name != 'vane-annotations' && it.name != 'vane-waterfall'}) {
	task copyJar(type: Copy) {
		from jar;
		into "${project.rootProject.projectDir}/target";
	}
	build.dependsOn copyJar;

	task copyJarAsc(type: Copy) {
		from signJar;
		into "${project.rootProject.projectDir}/target";
	}
	build.dependsOn copyJarAsc;

	dependencies {
		implementation group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '4.6.0-SNAPSHOT';

		compileOnly project(':vane-annotations');
		annotationProcessor project(':vane-annotations');
	}

	processResources {
		filesMatching("**/plugin.yml") {
			expand(project.properties)
		}
	}
}

configure(subprojects.findAll {it.name != 'vane-annotations' && it.name != 'vane-core' && it.name != 'vane-waterfall'}) {
	dependencies {
		implementation project(path: ':vane-core', configuration: 'shadow');
	}
}
