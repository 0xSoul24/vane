plugins {
	id 'org.ajoberstar.organize-imports' version '1.0.3' apply false;
}

subprojects {
	apply plugin: 'java';
	apply plugin: 'org.ajoberstar.organize-imports';

	group 'org.oddlama.vane';
	version '1.0.0';
	sourceCompatibility = 11;

	repositories() {
		mavenCentral();
		maven { url 'https://papermc.io/repo/repository/maven-public/' };
		maven { url 'https://repo.dmulloy2.net/nexus/repository/public/' };
		maven { url 'https://repo.mikeprimm.com/' };
		maven { url 'https://papermc.io/repo/repository/maven-public/' };
		maven { url 'https://repo.codemc.org/repository/maven-public/' };
		flatDir {
			dir rootProject.file('libs');
		}
	}

	compileJava {
		options.encoding = 'UTF-8';
		options.compilerArgs << '-Xlint:all';
		options.compilerArgs << '-Xlint:-processing';
		options.compilerArgs << '-Xdiags:verbose';
	}

	dependencies {
		//implementation group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.1-R0.1-SNAPSHOT';
		// We need the full implementation with CraftBukkit and NMS to compile.
		// Simply copy cache/patched_{..}.jar from a paper server.
		implementation name: 'patched_1.16.2';
		compileOnly group: 'org.jetbrains', name: 'annotations', version: '20.0.0';
		annotationProcessor 'org.jetbrains:annotations:20.0.0';
	}
}

configure(subprojects.findAll {it.name != 'vane-annotations'}) {
	dependencies {
		implementation group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '4.6.0-SNAPSHOT';
		implementation group: 'us.dynmap', name: 'dynmap-api', version: '3.1-SNAPSHOT';

		compileOnly project(':vane-annotations');
		annotationProcessor project(':vane-annotations');
	}

	processResources {
		filesMatching("**/plugin.yml") {
			expand(project.properties)
		}
	}
}

configure(subprojects.findAll {it.name != 'vane-annotations' && it.name != 'vane-core'}) {
	dependencies {
		implementation project(':vane-core');
	}
}
